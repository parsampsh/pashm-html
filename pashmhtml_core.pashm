namespace pashm_html
	func run_file($path)
		$f = fopen($path, 'r')
		$content = $f->read()
		$f->close()
		free $f $path
		return pashm_html.run($content)
	endfunc

	func run($content)
		$randstr_1 = '<<<therandomstringforpashmhtml' + str(time.time()) + str(random.random()) + '>>>'
		$randstr_2 = '<<<therandomstringforpashmhtml' + str(time.time()) + str(random.random()) + '>>>'
		$content = $content->replace('\{', $randstr_1)
		$content = $content->replace('\}', $randstr_2)
		$content = $content->replace('{{', '{=')
		$content = $content->replace('}}', '}')
		$parts = $content->split('{')
		$i = 0
		$new_parts = []
		section pashm_html_loop1
			$tmp = $parts[$i]->split('}', 1)
			if len($tmp) > 1
				$new_parts->append([True, $tmp[0]->replace($randstr_1, '{')->replace($randstr_2, '}')])
				$new_parts->append([False, $tmp[1]->replace($randstr_1, '{')->replace($randstr_2, '}')])
			else
				$new_parts->append([False, $tmp[0]->replace($randstr_1, '{')->replace($randstr_2, '}')])
			endif
			$i = $i + 1
		mem $i < len($parts); gotoif pashm_html_loop1
		
		$pashm_html_i = 0
		$pashm_html_parts = $new_parts
		free $i $new_parts $parts $content $randstr_1 $randstr_2 $tmp
		out_start()
		section pashm_html_loop2
			if $pashm_html_parts[$pashm_html_i][0] == True
				if $pashm_html_parts[$pashm_html_i][1]
					if $pashm_html_parts[$pashm_html_i][1][0] == '='
						eval('print(' + $pashm_html_parts[$pashm_html_i][1][1:] + ')')
					else
						eval($pashm_html_parts[$pashm_html_i][1])
					endif
				endif
			else
				print($pashm_html_parts[$pashm_html_i][1])
			endif
			$pashm_html_i = $pashm_html_i + 1
		mem $pashm_html_i < len($pashm_html_parts); gotoif pashm_html_loop2
		out_end()
		return out_get_clean()
	endfunc
endns
